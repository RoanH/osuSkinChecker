plugins{
	id 'eclipse'
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'edu.sc.seis.launch4j' version '2.5.1'
	id 'com.github.spotbugs' version '5.0.14'
}

sourceSets{
	main{
		java.srcDirs = ['src']
		resources.srcDirs = ['resources']
	}
}

dependencies{
	implementation 'dev.roanh.util:util:2.1'
}

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			allowInsecureProtocol = true
			url "$nexusPublic"
		}
	}
	mavenCentral()
}

version = findProperty("refName") ?: 'SNAPSHOT'
if(version.matches("v\\d+\\.\\d+")){
	version = version.substring(1)
}

eclipse.classpath.downloadSources = true
eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
javadoc.options.memberLevel = JavadocMemberLevel.PRIVATE
group = 'dev.roanh.osuskinchecker'
ext.artifact = 'osuskinchecker'
shadowJar.archiveName = 'SkinChecker-v' + version + '.jar'
application.mainClassName = 'dev.roanh.osuskinchecker.SkinChecker'

spotbugs{
	showProgress = true
	effort = 'max'
	reportLevel = 'low'
	excludeFilter = file("$rootDir/exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask){
	reports{
		html{
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

publishing{
	publications{
		mavenJava(MavenPublication){
			from components.java
			artifactId = project.artifact

			pom{
				name = project.eclipse.project.name
				description = 'Simple program to check for missing files in an osu! skin.'
				url = 'https://github.com/RoanH/osuSkinChecker'
				licenses{
					license{
						name = 'GNU General Public License v3.0'
						url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan@roanh.dev'
					}
				}
				scm{
					connection = 'scm:git:git@github.com:RoanH/osuSkinChecker.git'
					developerConnection = 'scm:git:git@git.roanh.dev:roan/osuSkinChecker.git'
					url = 'https://github.com/RoanH/osuSkinChecker'
				}
			}
		}
	}
}

shadowJar{
	into("META-INF/maven/$project.group/$project.artifact"){
		from{
			generatePomFileForMavenJavaPublication
		}
		rename ".*", "pom.xml"
	}
}

launch4j{
	jarTask = project.tasks.shadowJar
	mainClassName = application.mainClassName
	icon = "${projectDir}/skinchecker.ico"
	jreMinVersion = project.sourceCompatibility
	bundledJrePath = "%JAVA_HOME%"
	outfile = rootProject.name + '-v' + project.version + '.exe'
	fileDescription = "SkinChecker"
	productName = rootProject.name
	version = project.version
	textVersion = project.version
	copyright = "Roan Hofland"
}
